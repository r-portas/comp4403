package parser;

import java_cup.runtime.*;   // Library needed for Java-CUP for parsing
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.TreeMap;

import source.*;
import tree.DeclNode;
import tree.ConstExp;
import tree.StatementNode;
import tree.ExpNode;
import tree.Operator;
import syms.*;

/** PL0 definition for CUP LALR(1) parser generator.
 * @version $Revision: 22 $Date: 2013-05-08 13:58:21 +1000 (Wed, 08 May 2013)$
 * This module is responsible for parsing the input file in terms
 * of the tokens supplied by the lexical analyser (see PL0.flex),
 * and generating a symbol table skeleton and an abstract syntax 
 * tree for program.
 
 * Java-CUP will output a file "CUPParser.java" that contains the parser
 * for PL0. It also outputs CUPToken.java.
 * The name "CUPParser.java" is determined by the "parser" option and
 * the name "CUPToken.java" by the "symbols" option to the java_cup command 
 * line or to the corresponding ant task.
 */

parser code 
{:  /* This section provides some methods used by Java_CUP during parsing.
       They override its default methods for reporting syntax errors. */
        
    /** Retrieve the error handler to handle error messages. */
    private Errors errors = ErrorHandler.getErrorHandler();
    
    /** Override the default CUP syntax_error method with one 
     * that integrates better with the compiler's error reporting. */
    @Override
    public void syntax_error( Symbol cur_token ) {
        errors.error( "PL0 syntax error", ((ComplexSymbol) cur_token).xleft );
    }
    /** Override the default CUP unrecovered_syntax_error method with one 
     * that integrates better with the compiler's error reporting. */
    @Override
    public void unrecovered_syntax_error( Symbol cur_token ) {
        errors.error( "PL0 unrecovered syntax error", 
                        ((ComplexSymbol) cur_token).xleft );
    }
            
:}

action code 
{:  /* This section provides global variables and methods used in the
     * semantics actions associated with parsing rules.
     * These are the only global variables you should need. */

    /** Error handler for reporting error messages. */
    private Errors errors = ErrorHandler.getErrorHandler();
    
    /** Symbol table is available globally.
     *  Its current scope corresponds to the procedure/main program 
     *  being processed.
     */
    SymbolTable symtab;
:}


/*--------------------------------------------------------------*
 *--------------------- TERMINAL SYMBOLS -----------------------*
 *--------------------------------------------------------------*
 * CUP generates a file CUPToken.java containing the Java definitions 
 * generated by from the following list of terminal symbols.
 * CUPToken.java is used to communicate with the lexical analyser. 
 * The name "CUPToken.java" is determined by the "symbols" option to java_cup
 * (specified in command line invoking java-cup or in the corresponding ant 
 * task).
 * See PL0.flex for the detailed definitions of the lexical tokens.
 */
terminal
    LPAREN,       /* ( */
    RPAREN,       /* ) */
    SEMICOLON,    /* ; */
    COLON,        /* : */
    ASSIGN,       /* := */
    PLUS,         /* + */
    MINUS,        /* - */
    TIMES,        /* * */
    DIVIDE,       /* / */
    EQUALS,       /* = */
    NEQUALS,      /* != */
    LEQUALS,      /* <= */
    GEQUALS,      /* >= */
    LESS,         /* < */
    GREATER,      /* > */
    RANGE,        /* .. */
    PERIOD,       /* . */
    COMMA,        /* , */
    POINTER,      /* ^ */
    LCURLY,       /* { */
    RCURLY,       /* } */
    LBRACKET,     /* [ */
    RBRACKET,     /* ] */
    KW_BEGIN,     /* begin */
    KW_CALL,      /* call */
    KW_CONST,     /* const */
    KW_DO,        /* do */
    KW_ELSE,      /* else */
    KW_END,       /* end */
    KW_IF,        /* if */
    KW_NEW,       /* new */
    KW_PROCEDURE, /* procedure */
    KW_READ,      /* read */
    KW_RECORD,    /* record */
    KW_THEN,      /* then */
    KW_TYPE,      /* type */
    KW_VAR,       /* var */
    KW_WHILE,     /* while */
    KW_WRITE,     /* write */
    ILLEGAL;      /* illegal symbol - not used in grammar */

/** The following terminal symbols have an attribute associated with them.
 */
terminal String  IDENTIFIER;   /* identifier */
terminal Integer NUMBER;       /* number */

/**-------------------------------------------------------------*
 *------------------ NONTERMINAL SYMBOLS -----------------------*
 *--------------------------------------------------------------*
 * Many nonterminals (but not all) have an attribute type associated 
 * with them. This must be a Java class (subclass of Object, e.g., 
 * Integer rather than int).
 * This gives the type of the special attribute RESULT in the grammar 
 * rules in which the nonterminal appears on the left side of a grammar
 * rule, and the type of the attribute corresponding to the label 
 * attached to occurrences of the nonterminal on the right side of a rule.
 *
 * The attributes are used to construct the symbol table for the
 * declaration parts and the abstract syntax tree for the bodies of
 * procedures and the main program.
 */
non terminal DeclNode.ProgramNode Program;
non terminal StatementNode.BlockNode   Block;
non terminal DeclNode.DeclListNode DeclarationList;
non terminal                  Declaration;
non terminal                  ConstDefSeq, 
                              ConstDef;
non terminal ConstExp         Constant;
non terminal                  TypeDefSeq, 
                              TypeDef;
non terminal Type             Type,
                              TypeIdentifier;
non terminal                  VarDeclSeq, 
                              VarDecl;
non terminal DeclNode.ProcedureNode ProcedureDef;
non terminal Scope            ProcedureHead;
non terminal 
                              FormalParamList;
non terminal 
                              ActualParamList;
non terminal StatementNode.ListNode StatementList;
non terminal StatementNode    Statement,
                              CompoundStatement;
non terminal ExpNode          Condition,
                              RelCondition,
                              Exp,
                              Term,
                              Factor;
non terminal Operator         Relation,
                              AddOp,
                              MulOp,
                              UnaryOperator;
non terminal ExpNode          LValue;

non terminal ArrayList<Type.Field> Fields,
                              FieldList;

non terminal Type.Field       Field;

non terminal Type.PointerType PointerType;

// ExpLists should contain conditions, which are ExpNodes
non terminal ArrayList<ExpNode> ExpList,
                                RecordFields;

/**-------------------------------------------------------------*
 *------------------------ GRAMMAR RULES -----------------------*
 *--------------------------------------------------------------*
 * Grammar productions have actions associated with them, which are
 * executed after recognising (part of) the right side of the production.
 *  
 * The action is Java code contained between '{:' and ':}'
 * It is allowed to reference the values of attributes of symbols on 
 * the right side (the name of these attributes is the name after the 
 * ':' following a symbol, e.g., in the rule for Program the attribute 
 * associated with Block is 'block'), and assignments to the attribute of 
 * the left side nonterminal (which is always referred to as 'RESULT').
 *
 * Be careful to get the syntax of the '{:' and ':}' correct and to
 * make sure they match up, otherwise the error messages get very
 * confusing. Also don't forget the ";" at the end of a production.
 * 
 * To allow the locations of errors in the input source program to be
 * reported, each attribute, such as "id" associated with IDENTIFIER
 * in the production for ConstDef, has two further attributes "idxleft" 
 * and "idxright", representing the locations (think of a location as
 * a line and column number) of the leftmost and rightmost characters 
 * of the construct in the input source program.
 * Note that the name "idxleft" is constructed from the name "id" by
 * adding "xleft" to it. 
 *
 * Java-CUP has a special non-terminal symbol 'error', which is used to 
 * allow recovery from syntax errors in the program. 
 *
 * The action code usually comes at the end of the right side of a production,
 * however, it is possible to have multiple actions, each of which is executed
 * as soon as the symbols preceding it are matched.
 * For example, for Program the first thing on the right side is an 
 * action, and hence this action is executed before the right side of
 * program (namely Block) is recognised, i.e., before any of the 
 * Program is recognised. The second action following Block is executed
 * after the whole of the Block representing the program is recognised.
 */
Program ::=  /* empty */ 
        {:
            /* This action occurs before the whole program is recognised.
             * Construct initial symbol table with current scope the 
             * predefined scope. */
            symtab = new SymbolTable(); 
            /* Set up a dummy symbol table entry for the main program */
            SymEntry.ProcedureEntry proc = 
                symtab.getCurrentScope().addProcedure( "<main>", 
                    ErrorHandler.NO_LOCATION );
            if( proc  == null ) {
                errors.fatal( "Could not add main program to symbol table",
                    ErrorHandler.NO_LOCATION );
            }
            /* Enter the scope for the main program and save the new local
             * scope in main's symbol table entry */
            Scope blockLocals = symtab.newScope( proc );
            proc.setLocalScope( blockLocals );
        :}
     Block:block
        {: 
            /* This action is executed after the whole program 
             * has been recognised */
            block.setBlockLocals( symtab.getCurrentScope() );
            symtab.leaveScope();
            /* Returned result of the whole parsing phase */
            RESULT = new DeclNode.ProgramNode( symtab, block );
        :}
    | error
        {:
            /* A null result from Program will cause the compiler to
               avoid further processing. */
            RESULT = null;
        :}
    ;
Block ::= DeclarationList:dl CompoundStatement:b
        {: 
            RESULT = new StatementNode.BlockNode( bxright, dl, b );
        :}
    ;
DeclarationList ::= DeclarationList:dl ProcedureDef:p SEMICOLON
        {:
            dl.addDeclaration(p);
            RESULT = dl;
        :}
    | DeclarationList:dl Declaration
        {: 
            RESULT = dl;
        :}
    | /* empty */
        {:
            RESULT = new DeclNode.DeclListNode();
        :}
    ;

Declaration ::= KW_CONST ConstDefSeq
    |  KW_TYPE TypeDefSeq
    |  KW_VAR  VarDeclSeq
    ;

ConstDefSeq ::= ConstDef
    |  ConstDefSeq ConstDef
    ;

ConstDef ::= IDENTIFIER:id EQUALS Constant:c SEMICOLON
        {: 
            /* The attribute idxleft represents the location of the start
             * character of the IDENTIFIER token in the input stream.
             */
            /* addConstant returns null if id is already defined 
             * in the current scope */
            if( symtab.getCurrentScope().addConstant( id, idxleft, c ) == null ) {
                errors.error( id + " already defined", idxleft );
            }
        :}
    |  error 
    ;
/* The rules for Constant construct a (mini) abstract syntax tree 
 * for constant expressions (not to be confused with ExpNodes).
 */
Constant ::= NUMBER:n
        {: 
            RESULT = new ConstExp.NumberNode( nxleft, 
                    symtab.getCurrentScope(), Predefined.INTEGER_TYPE, n ); 
        :}
    | MINUS:op Constant:c
        {: 
            RESULT = new ConstExp.NegateNode( opxleft, 
                    symtab.getCurrentScope(), c ); 
        :}
    |  IDENTIFIER:id
        {: 
            RESULT = new ConstExp.ConstIdNode( idxleft, 
                    symtab.getCurrentScope(), id );
        :}
    |  error:err
        {:
            RESULT = new ConstExp.ErrorNode( errxleft,
                                             symtab.getCurrentScope() );
        :}
    ;

TypeDefSeq ::= TypeDef 
    |  TypeDefSeq TypeDef
    ;

TypeDef ::= IDENTIFIER:id EQUALS Type:type SEMICOLON
        {: 
            SymEntry.TypeEntry entry = 
                symtab.getCurrentScope().addType( id, idxleft, type );
            if( entry == null ) {
                errors.error( id + " already defined", idxleft );
            }
        :}
    |  error 
    ;

/** BEGIN MY CODE **/

Field ::= IDENTIFIER:i COLON TypeIdentifier:ti
        {:
            RESULT = new Type.Field(ixleft, i, ti);
        :}
    ;

/* Implementation based off StatementList */
Fields ::= Field:f
        {:
            ArrayList fields = new ArrayList<Type.Field>();
            fields.add(f);
            RESULT = fields;
        :}
    | Fields:fs SEMICOLON Field:f
        {:
            fs.add(f);
            RESULT = fs;
        :}
    ;

FieldList ::= Fields:fs
        {:
            RESULT = fs;
        :}
    | /* empty */
        {:
            RESULT = new ArrayList<Type.Field>();
        :}
    ;

Type ::= TypeIdentifier:type
        {: 
            RESULT = type;
        :}
    | LBRACKET Constant:lo RANGE Constant:hi RBRACKET
        {:
            RESULT = new Type.SubrangeType( lo, hi );
        :}
    | POINTER TypeIdentifier:ti
        {:
            // Check that the type is in the scope
            Type.PointerType pointer = new Type.PointerType(ti);

            /**
             * Equality and Inequality of pointers
             * =  PointerType(ti) x PointerType(ti)
             */

            // Roughly based on implementation in Predefined.java

            Type.ProductType pointerCrossPointer = new Type.ProductType(pointer, pointer);
            Type.ProductType pointerCrossNil = new Type.ProductType(pointer, Predefined.NIL_TYPE);
            Type.FunctionType pointerType = new Type.FunctionType(pointerCrossPointer, Predefined.BOOLEAN_TYPE);
            Type.FunctionType nilPointerType = new Type.FunctionType(pointerCrossNil, Predefined.BOOLEAN_TYPE);

            Scope curScope = symtab.getCurrentScope();
            SymEntry.OperatorEntry equal = curScope.lookupOperator("_=_");
            equal.extendType(pointerType);
            equal.extendType(nilPointerType);

            SymEntry.OperatorEntry notEqual = curScope.lookupOperator("_!=_");
            notEqual.extendType(pointerType);
            notEqual.extendType(nilPointerType);

            RESULT = pointer;

        :}
    | KW_RECORD FieldList:fl KW_END
        {:
            // TODO: Set location
            Type.RecordType record = new Type.RecordType();
            record.setLocation(flxleft);

            ArrayList<String> fieldIdentifiers = new ArrayList<String>();

            for (Type.Field f : ((ArrayList<Type.Field>)fl)) {
                String identifier = f.getId();

                if (fieldIdentifiers.contains(identifier)) {
                    errors.error("Duplicate record keys", flxleft);
                }

                fieldIdentifiers.add(identifier);
                record.add(f);
            }

            RESULT = record;
        :}
    | error:err
        {:
            RESULT = Type.ERROR_TYPE;
        :}
    ;

/** END MY CODE **/

TypeIdentifier ::= IDENTIFIER:id
        {: /* As the type identifier may not be defined at this point
            * the abstract syntax tree records the id, as well as the 
            * symbol table context to look it up within later.
            */
            RESULT = new Type.IdRefType( id, symtab.getCurrentScope(),
                                         idxleft );
        :}
    ;

VarDeclSeq ::= VarDecl
    |  VarDeclSeq VarDecl
    ;

VarDecl ::= IDENTIFIER:id COLON TypeIdentifier:type SEMICOLON
        {: 
            // Variables are always of ReferenceType.
            Type.ReferenceType varType = new Type.ReferenceType( type );
            if(symtab.getCurrentScope().addVariable(id, idxleft, varType) == null) {
                errors.error( id + " already declared", idxleft );
            }
        :}
    |  error
    ;
    
ProcedureDef ::= ProcedureHead:scope EQUALS Block:b
        {:
            b.setBlockLocals( scope );
            symtab.leaveScope();
            RESULT = new DeclNode.ProcedureNode( 
                (SymEntry.ProcedureEntry)scope.getOwnerEntry(), b );
        :}
    ;

ProcedureHead ::= KW_PROCEDURE IDENTIFIER:id
            LPAREN FormalParamList:pl RPAREN 
        {: 
            SymEntry.ProcedureEntry procEntry;
            /* Add an entry for the procedure to the current scope */
            procEntry = symtab.getCurrentScope().addProcedure( id, idxleft );
            if( procEntry == null ) {
                errors.error( id + " already declared", idxleft );
                /* Create a dummy symbol table entry.
                 * Note that this entry isn't in symtab. */
                procEntry = new SymEntry.ProcedureEntry( id, idxleft );
                procEntry.setScope( symtab.getCurrentScope() );
            }
            /* Create a new scope for the symbols local to
             * the procedure. */
            RESULT = symtab.newScope( procEntry );
            procEntry.setLocalScope( RESULT );
        :}
    |  KW_PROCEDURE IDENTIFIER:id error
        {: 
            SymEntry.ProcedureEntry procEntry = 
                new SymEntry.ProcedureEntry( id, idxleft );
            procEntry.setScope( symtab.getCurrentScope() );
            RESULT = symtab.newScope( procEntry );
            procEntry.setLocalScope( RESULT );
        :}
    ;


/* To allow for the addition of procedure parameters. */
FormalParamList ::= /* empty */
    ;

CompoundStatement ::= KW_BEGIN StatementList:sl KW_END
        {:
            RESULT = sl;
        :}
    ;

StatementList ::= Statement:s
        {:
            RESULT = new StatementNode.ListNode( sxleft );
            RESULT.addStatement(s);
        :}
    |  StatementList:sl SEMICOLON Statement:s
        {:
            sl.addStatement( s );
            RESULT = sl;
        :}
    ;

Statement ::= KW_WHILE Condition:c KW_DO Statement:s
        {: 
            RESULT = new StatementNode.WhileNode( cxleft, c, s );
        :}
    |  KW_IF Condition:c KW_THEN Statement:s1 KW_ELSE Statement:s2
        {: 
            RESULT = new StatementNode.IfNode( cxleft, c, s1, s2 );
        :}
    |  CompoundStatement:s
        {: 
            RESULT = s; 
        :}
    | KW_READ:r LValue:lval
        {: 
            RESULT = new StatementNode.AssignmentNode( lvalxleft, 
                     lval, new ExpNode.ReadNode( rxleft ) );
        :}
    |  KW_WRITE Exp:e
        {: 
            RESULT = new StatementNode.WriteNode( exleft, e );
        :}
    | LValue:lval ASSIGN Condition:rval
       {:
          RESULT = new StatementNode.AssignmentNode( lvalxleft, lval, rval );
       :} 
    |  KW_CALL IDENTIFIER:id LPAREN ActualParamList:pl RPAREN
        {: 
            RESULT = new StatementNode.CallNode( idxleft, id );
        :}
    |  error:p 
        {: 
            RESULT = new StatementNode.ErrorNode( pxleft );
        :}
    ;

ActualParamList ::= /* empty */
    ;

LValue ::= IDENTIFIER:id
        {:
            // Type of identifier is error?
            RESULT = new ExpNode.IdentifierNode( idxleft, id );
        :}
    | LValue:lv POINTER
        {:
            // Create a deref pointer type
            ExpNode.DerefPointerNode pointer = new ExpNode.DerefPointerNode(lvxleft, lv);

            RESULT = pointer;
        :}
    | LValue:lv PERIOD IDENTIFIER:id
        {:
            RESULT = new ExpNode.RecordReferenceNode(lvxleft, lv, id);

            /* ExpNode.RecordNode record = new ExpNode.RecordNode(lvxleft); */
            /* record.addRecordField(id); */
            /* RESULT = record; */
        :}
    ;

/* To allow for adding logical expressions. */
Condition ::= RelCondition:e
        {: 
            RESULT = e;
        :}
    ;
/* Relational operators are lower precedence than arithmetic operators. */
RelCondition ::= Exp:e 
        {:
            RESULT = e;
        :}
    |  Exp:e1 Relation:op Exp:e2
        {:
            RESULT = new ExpNode.OperatorNode( opxleft,
                op, new ExpNode.ArgumentsNode(e1,e2) );
        :} 
    ;

Relation ::= EQUALS
        {:
            RESULT = Operator.EQUALS_OP;
        :}
    |  NEQUALS
        {:
            RESULT = Operator.NEQUALS_OP;
        :}
    |  LEQUALS
        {:
            RESULT = Operator.LEQUALS_OP;
        :}
    |  LESS
        {:
            RESULT = Operator.LESS_OP;
        :}
    |  GREATER
        {:
            RESULT = Operator.GREATER_OP;
        :}
    |  GEQUALS
        {:
            RESULT = Operator.GEQUALS_OP;
        :}
    ;
/* Expressions are structured into Exp, Term, and Factor to encode the
 * precedence of the operators in the grammar.
 */
Exp ::= Term:t
        {:
            RESULT = t;
        :}
    |  Exp:e1 AddOp:op Term:e2
        {:
            RESULT = new ExpNode.OperatorNode( opxleft, op,
                new ExpNode.ArgumentsNode(e1,e2) );
        :}
    ;

AddOp ::= PLUS
        {:
            RESULT = Operator.ADD_OP;
        :}
    |  MINUS
        {:
            RESULT = Operator.SUB_OP;
        :}
    ;

Term ::= Factor:f
        {:
            RESULT = f;
        :}
    |  Term:e1 MulOp:op Factor:e2
        {:
            RESULT = new ExpNode.OperatorNode( opxleft, op,
                new ExpNode.ArgumentsNode(e1,e2) );
        :}
    ;

MulOp ::= TIMES
        {:
            RESULT = Operator.MUL_OP;
        :}
    |  DIVIDE
        {:
            RESULT = Operator.DIV_OP;
        :}
    ;

/* Implementation based on StatementList */
ExpList ::= Condition:c
        {:
            ArrayList<ExpNode> exps = new ArrayList<ExpNode>();
            exps.add(c);
            RESULT = exps;
        :}
    | ExpList:el COMMA Condition:c
        {:
            el.add(c);
            RESULT = el;
        :}
    ;

RecordFields ::= /* empty */ 
        {:
            RESULT = new ArrayList<ExpNode>();
        :}
    | ExpList:el
        {:
            RESULT = el;
        :}
    ;

Factor ::= PLUS Factor:e
        {:
            RESULT = e; 
        :}
    |  UnaryOperator:op Factor:e
        {:
            RESULT = new ExpNode.OperatorNode( opxleft, op, e );
        :}
    |  LPAREN Condition:c RPAREN
        {:
            RESULT = c;
        :}
    |  NUMBER:n
        {:
            RESULT = new ExpNode.ConstNode( nxleft, 
                    Predefined.INTEGER_TYPE, n.intValue() );
        :}
    |  LValue:lval
        {:
            RESULT = lval;
        :}
    | KW_NEW TypeIdentifier:ti
        {:
            // Check that ti is a pointer type
            SymEntry.TypeEntry type = symtab.getCurrentScope().lookupType(ti.getName());
            if (type == null || !(type.getType() instanceof Type.PointerType)) {
                errors.error("Cannot create a new instance of a type that does not exist", tixleft);
            }

            ExpNode.PointerNode pointer = new ExpNode.PointerNode(tixleft,
                new Type.PointerType(ti));
            pointer.setPointerType(ti);

            RESULT = pointer;
        :}
    | TypeIdentifier:ti LCURLY RecordFields:rf RCURLY
        {:
            // TODO: Might be incorrect
            ExpNode.RecordNode node = new ExpNode.RecordNode(tixleft, ti);
            node.addRecordFields(rf);

            RESULT = node;
        :}
    | error:e
        {:
            RESULT = new ExpNode.ErrorNode( exleft );
        :}
    ;
UnaryOperator ::= MINUS:op
        {:
            RESULT = Operator.NEG_OP;
        :}
    ;

